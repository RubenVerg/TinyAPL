⍝⍝ std:math: Mathematical functions

Circular ↚ {
  ⍺=  0: ■√1-⍵*2
⋄ ⍺=  1: ■⎕math→Sin ⍵
⋄ ⍺= ¯1: ■⎕math→Arcsin ⍵
⋄ ⍺=  2: ■⎕math→Cos ⍵
⋄ ⍺= ¯2: ■⎕math→Arccos ⍵
⋄ ⍺=  3: ■⎕math→Tan ⍵
⋄ ⍺= ¯3: ■⎕math→Arctan ⍵
⋄ ⍺=  4: ■√1+⍵*2
⋄ ⍺= ¯4: ■√1-⍨⍵*2
⋄ ⍺=  5: ■⎕math→Sinh ⍵
⋄ ⍺= ¯5: ■⎕math→Arsinh ⍵
⋄ ⍺=  6: ■⎕math→Cosh ⍵
⋄ ⍺= ¯6: ■⎕math→Arcosh ⍵
⋄ ⍺=  7: ■⎕math→Tanh ⍵
⋄ ⍺= ¯7: ■⎕math→Artanh ⍵
⋄ ⍺=  8: ■√-1+⍵*2
⋄ ⍺= ¯8: ■-√-1+⍵*2
⋄ ⍺=  9: ■ℜ⍵
⋄ ⍺= ¯9: ■⍵
⋄ ⍺= 10: ■|⍵
⋄ ⍺=¯10: ■+⍵
⋄ ⍺= 11: ■ℑ⍵
⋄ ⍺=¯11: ■⊕⍵
⋄ ⍺= 12: ■∡⍵
⋄ ⍺=¯12: ■⊗⍵
⋄ 2⨳"unknown circular function"
}

⍝⍝ A function that replicates the behavior of the Circular function from Dyalog and most other APLs.
Circle ⇇ (⎕math→pi∘×)⁖(Circular⌓)

⍝⍝ The primitive ⍵-th root of unity
RootOfUnity ⇇ ⦅⊗⋄2×⎕math→pi⋄×⋄÷⦆

⍝ Constants

⍝⍝ π, the ratio of the radius of a circumference and its length
pi ⇇ ⎕math→pi

⍝⍝ τ, the ratio of the diameter of a circumference and its length
tau ⇇ 2×⎕math→pi

⍝⍝ η, the ratio of the radius of a circumference and its half its length
eta ⇇ 2÷⍨⎕math→pi

⍝⍝ e, Euler's constant
e ⇇ *1

⍝⍝ φ, the golden ratio
phi ⇇ 1.6180339887498948482045868343656381177203091798057628621354486227

⍝⍝ i, the imaginary unit
i ⇇ 0ᴊ1

⍝⍝ ω, the primitive cube root of unity
omega ⇇ 2÷⍨¯1⊕√3 ⟃ RootOfUnity is imprecise ⟄
